public class BobDiscoverer [principal P] implements Discoverer [P] authority (P){
    public DiscoveryResult [q] {q->} discover {P <-;q<-;server<-}
    (Profile[{P ->; P <-}] {} x,
     principal  {} q,
     Profile[{q ->; q<-} ] {} y){
	Profile {q->} prof = generateProfile(x,q);
        
	if(isWithin3Years(x,q,y)){    
	    return new DiscoveryResult[q](true,prof);    
	}
	return new DiscoveryResult[q](false,null);
	
    }
    
    private boolean {q->} isWithin3Years{P <-;q<-;server<-}
    (Profile[{P ->; P <-}] {} x,
     principal{} q,
     Profile[{q ->; q<-} ] {} y)
     where authority(P){
	if(x==null || y==null)
	    return false;
	int{q->} otherAge = y.yyyy;
	int{q->} bobAge = declassify(x.yyyy,{P:} to {});
	int{q->} diff = otherAge-bobAge;	
	try{
	    return (diff*diff)/diff<=3;	    
	}catch(ArithmeticException e){
	    return 0==diff;
	}
    }

    private Profile [{q->}] {q->} generateProfile{P <-;q<-;server<-}
    (Profile[{P ->; P <-}] {} x,
     principal  {} q)
	where authority (P){
	Profile [{q->}] {q->} prof = new Profile();
	if(x==null)
	    return prof;
	prof.name = declassify(x.name,{P:} to {});
        prof.yyyy = declassify(x.yyyy,{P:} to {});
        return prof;	
    }
}
